buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.json:json:20160212'
    }
}

plugins {
    id 'java'
    id 'groovy'
    id 'application'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

compileTestJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

repositories {
    mavenCentral()
}

// In this section you declare the dependencies for your production and test code
dependencies {
    implementation fileTree(dir: "${project.rootDir}/lib", includes: ['*.jar'])

    implementation 'org.eclipse.jetty:jetty-server:11.0.15'
    implementation 'org.eclipse.jetty:jetty-servlet:11.0.15'
    implementation 'org.eclipse.jetty:jetty-webapp:11.0.15'

    implementation 'org.glassfish.jersey.core:jersey-server:3.1.5'
    implementation 'org.glassfish.jersey.containers:jersey-container-servlet:3.1.5'
    implementation 'org.glassfish.jersey.inject:jersey-hk2:3.1.5'
    implementation 'org.glassfish.jersey.media:jersey-media-multipart:3.1.5'
    implementation 'org.glassfish.jersey.media:jersey-media-json-binding:3.1.5'

    implementation 'jakarta.ws.rs:jakarta.ws.rs-api:3.1.0'
    implementation 'jakarta.servlet:jakarta.servlet-api:5.0.0'
    implementation 'jakarta.annotation:jakarta.annotation-api:2.1.1'
    implementation 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.0'

    implementation 'org.reflections:reflections:0.9.10'
    implementation 'org.json:json:20160212'
    implementation 'org.codehaus.groovy:groovy-all:3.0.9'

    testImplementation 'junit:junit:4.12'
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
            exclude 'DUKIntegrator/**'
        }
    }
}

mainClassName = "ro.incremental.anaf.declaratii.JobQueueServer"

distTar {
    compression = Compression.GZIP
}

distributions {
    main {
        contents {
            from('config') {
                into "config"
            }

            from('examples') {
                into 'examples'
            }
            from('ajutor') {
                into 'ajutor'
            }
        }
    }
}

task stage {
    dependsOn installDist
}

task updateFromANAF {
    doFirst {

        //Download an unpack sources
        // https://static.anaf.ro/static/DUKIntegrator/dist_javaInclus20200203.zip
        // ant.get(src: 'http://static.anaf.ro/static/10/Anaf/Declaratii_R/AplicatiiDec/src_20140505.zip', dest: 'DUKIntegrator/src.zip')
        // ant.unzip(src: 'DUKIntegrator/src.zip', dest: 'DUKIntegrator')

        // delete('DUKIntegrator/src.zip')

        //Download libraries
        def versionDictionary = [:]

        def configFile = new File('config/versiuniCurente.txt')

        // old versions xml url -> http://static.anaf.ro/static/10/Anaf/update1/versiuni.xml -> integrator version = 1.3.15.3.3
        // new versions xml url -> https://static.anaf.ro/static/10/Anaf/update5/versiuni.xml -> integrator version = 1.4.18.3.3
        def versiuni = new XmlSlurper().parse('https://static.anaf.ro/static/10/Anaf/update5/versiuni.xml')

        def allUrls = []

        //Itext and dependencies
        versiuni.integrator.iJars.childNodes().each({
            allUrls.add(it.text())
        })

        //Validator and dependencies
        versiuni.integrator.sJars.childNodes().each({
            allUrls.add(it.text())
        })

        //DUK and dependencies
        versiuni.integrator.zJars.childNodes().each({
            allUrls.add(it.text())
        })

        //Download and dependencies
        versiuni.integrator.dJars.childNodes().each({
            allUrls.add(it.text())
        })

        //Get declaratii URL's
        versiuni.declaratii.childNodes().each({
            def name = it.name()

            //Flatten library XML
            def library = it.children().inject([:], { acc, value ->

                acc[value.name()] = value.text()
                acc

            })

            allUrls.add(library.JURL)
            allUrls.add(library.PURL)

            versionDictionary[name] = library
        })

        //Download all url's
        allUrls.each({
            println it
            ant.get(src: it, dest: 'lib')
        })

        def packages = new File("src/main/resources/packages.txt")
        configFile.write "Declaratie;VersiuneJ;VersiuneP\n"

        versionDictionary.each { key, value ->
            configFile << "${key};${value.versiuneJ};${value.versiuneJ}\n"
            packages << "${key.toString().toLowerCase()}\n"
        }

    }
}
